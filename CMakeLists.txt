cmake_minimum_required(VERSION 3.17)

project(access_control_app VERSION 0.1.0)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(/opt/gaia/cmake/gaia.cmake)

find_package(Threads REQUIRED)

# Location where files generated by Gaia build tools are placed.
set(GENERATED_OUTPUTS "${PROJECT_BINARY_DIR}/generated")
file(MAKE_DIRECTORY ${GENERATED_OUTPUTS})

# Generate C++ headers from the .ddl schema file.
process_schema(
  DDL_FILE ${PROJECT_SOURCE_DIR}/src/access_control.ddl
  OUTPUT_FOLDER ${GENERATED_OUTPUTS}
  TARGET_NAME access_control_generated_headers
)

# Translate the declarative .ruleset file into a C++ source file.
translate_ruleset(
  RULESET_FILE ${PROJECT_SOURCE_DIR}/src/access_control.ruleset
  OUTPUT_FOLDER ${GENERATED_OUTPUTS}
  TARGET_NAME access_control_translated_ruleset
  CLANG_PARAMS
    -I ${PROJECT_SOURCE_DIR}/include
    -I ${GENERATED_OUTPUTS}
  DEPENDS access_control_generated_headers
)

# Get the Gaia shared library.
add_library(gaia SHARED IMPORTED GLOBAL)
set_target_properties(gaia PROPERTIES IMPORTED_LOCATION /opt/gaia/lib/libgaia.so)

add_executable(access_control
  src/main.cpp
  src/helpers.cpp
  src/actions.cpp
  src/ui_messaging.cpp
  ${GENERATED_OUTPUTS}/access_control_ruleset.cpp
)

target_include_directories(access_control
  PUBLIC
    ${PROJECT_SOURCE_DIR}/include
  PRIVATE
    /opt/gaia/include
    ${GENERATED_OUTPUTS}
)

target_link_libraries(access_control
  PRIVATE
    gaia
    Threads::Threads
)

add_dependencies(access_control
  access_control_generated_headers
  access_control_translated_ruleset
)
